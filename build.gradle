plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'info.jemsit'
version = '1.0.0'
description = 'Common contracts for SimSim project'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
ext {
    springBootVersion = '3.5.6'
    springCloudVersion = '2023.0.0'
}

repositories {
    mavenCentral()
}

dependencies {
    // Only API dependencies (no implementation)
    api 'org.springframework.cloud:spring-cloud-starter-openfeign'
    api 'org.springframework.boot:spring-boot-starter-validation'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    api 'com.fasterxml.jackson.core:jackson-annotations'
    api 'com.fasterxml.jackson.core:jackson-core'
    api 'com.fasterxml.jackson.core:jackson-databind'

}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'info.jemsit'
            artifactId = 'common-contracts'
            version = '1.1.2'

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/SimSimOpen/sim-sim-common-contracts"
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
    }
}
